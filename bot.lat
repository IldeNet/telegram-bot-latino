incluir ('ajustes')
incluir ('urls')

URL = 'https://api.telegram.org/bot' . api_key . '/'

funcion actualizaciones()
	res = consultar(URL . "getUpdates")
	# escribir(res)	# retira el primer signo para testear
	offset = json(res, ".result[0] .update_id")
	si ( offset != "" )
    si offset > 0
      offset = offset + 1
			consultar(URL . "getUpdates?offset=" . offset)	#borro el ultimo mensaje
    fin
  fin
  retorno res # regreso la consulta inicial
fin

funcion enviarMensaje(chatId, texto)
  retorno consultar(URL . "sendMessage?chat_id=" . chatId . "&text=" . codificar(texto))
fin

funcion enviarMensaje_markdown(chatId, texto)
  retorno consultar(URL . "sendMessage?chat_id=" . chatId . "&text=" . codificar(texto) . "&parse_mode=markdown")
fin

funcion enviarMensaje_html(chatId, texto)
  retorno consultar(URL . "sendMessage?chat_id=" . chatId . "&text=" . codificar(texto) . "&parse_mode=html")
fin

funcion enviarAccion(chatId, accion)
  accion = reemplazar(accion, "escribiendo", "typing")
  accion = reemplazar(accion, "subiendo_foto", "upload_photo")
  accion = reemplazar(accion, "subiendo_video", "upload_video")
  accion = reemplazar(accion, "subiendo_audio", "upload_audio")
  accion = reemplazar(accion, "subiendo_documento", "upload_document")
  accion = reemplazar(accion, "grabando_video", "record_video")
  accion = reemplazar(accion, "grabando_audio", "record_audio")
  accion = reemplazar(accion, "buscar_localizacion", "find_location")
  retorno consultar(URL . "sendChatAction?chat_id=" . chatId . "&action=" . accion)
fin


funcion reenviarMensaje(chatId, userId, msgId)
  retorno consultar(URL . "forwardMessage?chat_id=" . chatId . "&from_chat_id=" . userId . "&message_id=" . msgId)
fin

funcion abandonarChat(chatId)
  retorno consultar(URL . "leaveChat?chat_id=" . chatId)
fin

funcion expulsarUsuario(chatId, userId)
	consultar(URL . "kickChatMember?chat_id=" . chatId . "&user_id=" . userId)
	consultar(URL . "unbanChatMember?chat_id=" . chatId . "&user_id=" . userId)
fin

funcion banearUsuario(chatId, userId)
  retorno consultar(URL . "kickChatMember?chat_id=" . chatId . "&user_id=" . userId)
fin

funcion desbanearUsuario(chatId, userId)
  retorno consultar(URL . "unbanChatMember?chat_id=" . chatId . "&user_id=" . userId)
fin

enviarMensaje_markdown(administrador, "*Bot iniciado*")

mientras verdadero
  res = actualizaciones()
	# escribir(res)	# para testear descomenta la linea
	texto = json(res, '.result[0] .message .text')
  usuario_id = json(res, ".result[0] .message .from .id")
  usuario_nombre = json(res, ".result[0] .message .from .first_name")
  chat_id = json(res, ".result[0] .message .chat .id")
  chat_titulo = json(res, ".result[0] .message .chat .title")
  nuevo_usuario = json(res, ".result[0] .message .new_chat_member")
  nuevo_usuario_id = json(res, ".result[0] .message .new_chat_member .id")
  nuevo_usuario_nombre = json(res, ".result[0] .message .new_chat_member .first_name")

  si ( texto == "/start" )
  	enviarMensaje_markdown(chat_id, "¬°Hola *".usuario_nombre."*!\nEst√°s probando el nuevo bot escrito completamente en la programaci√≥n Latino.\n Por el momento faltan cosas. Pero de poco a poco ir√° mejorando üòÑüëç")
  fin

  si ( texto == "/id" )
  	enviarMensaje_markdown(chat_id, "*Id* ".usuario_id."\n*Chat Id* ".chat_id)
  fin

  si ( nuevo_usuario != "" )
  	enviarMensaje(chat_id, "Hola ".nuevo_usuario_nombre.", bienvenido a ".chat_titulo)
  fin

  si ( texto != "")
  	escribir(texto." desde ".usuario_nombre)
  fin

fin
